pluginManagement {
    repositories {
        maven {
            name = "Luke's Maven"
            url = 'https://maven.lukebemish.dev/releases'
        }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/'
        }
        maven {
            name = 'Architectury'
            url "https://maven.architectury.dev/"
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'org.gradlex.extra-java-module-info' version '1.8' apply false
    id 'dev.lukebemish.conventions' version '0.1.11'
    id 'dev.lukebemish.multisource' version '0.1.8'
}

gradle.beforeProject {
    it.plugins.apply('org.gradlex.extra-java-module-info')
    it.extraJavaModuleInfo {
        failOnMissingModuleInfo.set(false)
        automaticModule('dev.lukebemish.autoextension:autoextension', 'autoextension')
        automaticModule('com.mojang:datafixerupper', 'datafixerupper')
    }
}

multisource.of(':') {
    repositories {
        maven {
            name = "Terraformers"
            url = "https://maven.terraformersmc.com/"
            content {
                includeModule 'com.terraformersmc', 'modmenu'
            }
        }
    }
    configureEach {
        minecraft.add project.libs.minecraft
        mappings.add loom.officialMojangMappings()
    }
    common('minecraft', []) {}
    fabric('minecraftFabric', ['minecraft']) {}
    neoforge('minecraftNeoforge', ['minecraft']) {
        neoForge.add project.libs.neoforge
    }
    common('testCommon', []) {}
    neoforge('testNeoforge', ['testCommon']) {
        neoForge.add project.libs.neoforge
    }
    fabric('testFabric', ['testCommon']) {}
    repositories {
        it.removeIf { it.name == 'Forge' }
    }
}

rootProject.name = 'codecextras'
